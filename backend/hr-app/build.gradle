plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.openapi.generator' version '7.5.0'
}

group = 'com.razvanpotra'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for HR-APP'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}


dependencies {

    // === Application starters ===
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-kafka'
    implementation 'org.springframework.boot:spring-boot-starter-liquibase'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.3'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.15.3'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.3'
    implementation 'org.postgresql:postgresql:42.7.8'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
//    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
//    implementation 'org.springframework.boot:spring-boot-starter-security'

    // === MapStruct ===
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    // === Lombok (main) ===
    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'

    // (Optional) Better Lombok-MapStruct interop if you use Lombok builders/accessors elsewhere
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    // === Testing ===
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testImplementation 'org.testcontainers:postgresql:1.19.7'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'


    // MapStruct / Lombok processors for test sources too
    testImplementation 'org.mapstruct:mapstruct:1.5.5.Final'
    testAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    testCompileOnly 'org.projectlombok:lombok:1.18.32'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.32'

    // (Optional) binding for tests as well
    testAnnotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
}

tasks.named('test') {
    useJUnitPlatform()
}

openApiGenerate {
    generatorName = 'spring'
    inputSpec = "$rootDir/src/main/resources/openapi.yaml"
    outputDir = "$buildDir/generated-sources/openapi"
    apiPackage = 'com.razvanpotra.hr_app.controller'
    modelPackage = 'com.razvanpotra.hr_app.model'
    configOptions = [
            interfaceOnly : 'true',
            useSpringBoot3: 'true',
            apiNameSuffix : 'Api',
            groupByTags   : 'true',
            useTags       : "true",
    ]
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated-sources/openapi/src/main/java"
        }
    }
}